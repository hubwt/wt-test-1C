

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьПанельКартинок();
КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	ОбновитьПанельКартинок();
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Объект.Код <> ТекущийОбъект.Код Тогда
		ОбновитьПанельКартинок();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОписанияФайлов = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	// Удаляем временные файлы 
	ФайлыКУдалению = Новый Массив(); 
	Для Каждого ОписаниеФайла Из ОписанияФайлов Цикл
	 	ИмяВрем = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеФайла.Значение, "ИмяФайлаЛок", Неопределено);
	 	Если ИмяВрем <> Неопределено Тогда
	 		ФайлыКУдалению.Добавить(ИмяВрем)
	 	КонецЕсли; 
	КонецЦикла;
	Если ФайлыКУдалению.Количество() <> 0 Тогда
		Попытка
			УдалитьФайлы(ФайлыКУдалению);
		Исключение
		
		КонецПопытки;
	КонецЕсли;
	
	
КонецПроцедуры




&НаСервере
Процедура ОбновитьПанельКартинок()
	
	ЭлементыКУдалению = Новый Массив();
	
	Для каждого Элемент Из Элементы.ГруппаКартинки.ПодчиненныеЭлементы Цикл
		
		ЭлементыКУдалению.Добавить(Элемент);
		                                               
	КонецЦикла;
	
	Для Индекс = 0 По ЭлементыКУдалению.Количество() - 1 Цикл
		Элементы.Удалить(ЭлементыКУдалению[Индекс]);
	КонецЦикла;
	
	КаталогКартинок = Константы.дт_КаталогКартинокТМЦ.Получить();
	Если Не ЗначениеЗаполнено(КаталогКартинок) Тогда
		Возврат
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.Код) Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ СтрЗаканчиваетсяНа(КаталогКартинок, "\") Тогда
		КаталогКартинок = КаталогКартинок + "\";
	КонецЕслИ;	
	
	КодОбъекта = СокрЛП(Объект.Код);
	
	КаталогКартинок = КаталогКартинок + КодОбъекта;
	
	Картинки = дт_ФайлыНаДиске.ПолучитьСписокКартинок(КаталогКартинок, КодОбъекта);
	
	ОписанияФайлов = Новый Структура();
	
	Для Индекс = 0 По Картинки.Количество() - 1 Цикл
		Файл = Картинки[Индекс];
		ДобавитьКартинку(Файл.ПолноеИмя, Файл.Имя, Индекс);
	КонецЦикла;
	
	
	
КонецПроцедуры



&НаСервере
Процедура ДобавитьКартинку(ИмяФайла, ИмяСокращенное, Индекс)
	
	СтрокаУИ = СтрШаблон("Картинка%1", Индекс);//СтрЗаменить(ИмяСокращенное, ".", "_");
	Элемент = Элементы.Добавить(СтрокаУИ, Тип("ДекорацияФормы"), Элементы.ГруппаКартинки);
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	Элемент.Гиперссылка = Истина;
	Элемент.Заголовок = СтрШаблон("Фото %1", Индекс + 1); //ВыборкаДетальныеЗаписи.Представление;
	Элемент.УстановитьДействие("Нажатие", "ОткрытьКартинку");
	
	ОписанияФайлов.Вставить(Элемент.Имя, Новый Структура("ИмяФайлаСервер", ИмяФайла));
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьКартинку(Элемент)
	
	#Если ВебКлиент Тогда
	
	ПоказатьПредупреждение(, "В режиме веб-клиента функция не поддерживается");
		
	#Иначе
	
	Отказ = Ложь;
	
	ОписаниеФайла = ОписанияФайлов[Элемент.Имя];
	ИмяФайла = ОписаниеФайла.ИмяФайлаСервер;
	
	ИмяВрем = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеФайла, "ИмяФайлаЛок", Неопределено);
	 
	Если ИмяВрем = Неопределено Тогда
	
		АдресФайла = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
		ПолучитьФайлССервера(АдресФайла, ИмяФайла, Отказ);
	
		Если Отказ Тогда 
			Возврат
		КонецЕсли;
	
		Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла);
		ИмяВрем = ПолучитьИмяВременногоФайла(Расширение);
		ПолучитьФайл(АдресФайла, ИмяВрем, Ложь);
		ОписаниеФайла.Вставить("ИмяФайлаЛок", ИмяВрем);
			
	КонецЕсли;
		
	
	
	Ф = Новый Файл(ИмяВрем);
	
	Если Ф.Существует() Тогда
		
		ЗапуститьПриложение(ИмяВрем, , Ложь);
		
	КонецЕсли;
	
	#КонецЕсли
		
КонецПроцедуры



&НаСервере
Процедура ПолучитьФайлССервера(Адрес, ИмяФайла, Отказ)
	
	Попытка
		
		ДвДанные = Новый ДвоичныеДанные(ИмяФайла);
		ПоместитьВоВременноеХранилище(ДвДанные, Адрес);
			
	Исключение
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон("Не удалось получить файл с сервере %1. %2",
				ИмяФайла,
				ОписаниеОшибки()),
			,
			,
			,
			Отказ
		);
		
	КонецПопытки;
		
КонецПроцедуры






