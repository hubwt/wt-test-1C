
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	
	Если ТипЗнч(Параметры.СтрокаПоиска) <> Тип("Строка") Тогда
		Возврат
	КонецЕсли;
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Параметры.СтрокаПоиска) Тогда
		
		СтандартнаяОбработка = Ложь;
	
		ДанныеВыбора = Новый СписокЗначений();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Спр.Наименование КАК Наименование,
		|	Спр.Ссылка КАК Ссылка,
		|	Спр.Представление КАК Представление
		|ИЗ
		|	Справочник.Стеллаж КАК Спр
		|ГДЕ
		|	Спр.Наименование ПОДОБНО &Код";
		
		// отборы
		Если ТипЗнч(Параметры.Отбор) = Тип("Структура") Тогда
			Для каждого ПолеОтбора Из Параметры.Отбор Цикл
				
				ТипСравнения = ?(ТипЗнч(ПолеОтбора.Значение) = Тип("Массив") 
						ИЛИ ТипЗнч(ПолеОтбора.Значение) = Тип("ФиксированныйМассив")
						ИЛИ ТипЗнч(ПолеОтбора.Значение) = Тип("СписокЗначений"),
					"В",
					"=");
				
				Запрос.Текст = Запрос.Текст + "
				|	И " + СтрШаблон("Спр.%1 %2 &%3", ПолеОтбора.Ключ, ТипСравнения, ПолеОтбора.Ключ);
				
				Запрос.УстановитьПараметр(ПолеОтбора.Ключ, ПолеОтбора.Значение);
			
			КонецЦикла;
		КонецЕсли;
		
		
		Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Спр.Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Код", "%" + Параметры.СтрокаПоиска + "%");
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ДанныеВыбора.Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Представление);
		КонецЦикла;
		
		
		
		
	КонецЕсли;
	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти

#КонецЕсли