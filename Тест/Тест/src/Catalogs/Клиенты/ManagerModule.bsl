#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция получает значения реквизитов выбранного контрагента.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Ссылка на контрагента
//
// Возвращаемое значение:
//  Структура - реквизиты выбранного контрагента
//
Функция РеквизитыКонтрагента(Контрагент) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Контрагенты.ПолноеНаименование КАК СТРОКА(255)) КАК НаименованиеПолное,
	|	Контрагенты.ФИО КАК ФИО,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП,
	|	Контрагенты.КодПоОКПО КАК КодПоОКПО,
	|	Контрагенты.ОГРН КАК ОГРН,
	|	Контрагенты.ТипКлиента КАК ЮрФизЛицо,
	|	Контрагенты.ЮридическийАдрес КАК ЮридическийАдрес,
	|	Контрагенты.ФактическийАдрес КАК ФактическийАдрес,
	|	Контрагенты.Телефон КАК Телефоны,
	|	Контрагенты.Страна2 КАК СтранаДоставки,
	|	Контрагенты.Область КАК РегионДоставки,
	|	Контрагенты.Город2 КАК ГородДоставки,
	|	Контрагенты.Паспорт КАК Паспорт,
	|	Контрагенты.Прописка КАК Прописка,
	|	Контрагенты.Email КАК Email
	|ИЗ
	|	Справочник.Клиенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураРеквизитов = Новый Структура("Представление,Наименование,НаименованиеПолное,ИНН,КПП,КодПоОКПО,ОГРН,ЮрФизЛицо,ЮридическийАдрес,ФактическийАдрес,Телефоны,АдресДоставки,Паспорт,Прописка,Email");
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
		
		СтруктураРеквизитов.Наименование = Выборка.НаименованиеПолное;
		
		Если ОбщегоНазначенияУТКлиентСервер.ЭтоИП(Выборка.ЮрФизЛицо) 
			И Не ЗначениеЗаполнено(СтруктураРеквизитов.Наименование) Тогда
			СтруктураРеквизитов.Наименование = "Индивидуальный предприниматель " + Выборка.ФИО;
		ИначеЕсли ОбщегоНазначенияУТКлиентСервер.ЭтоФизлицо(Выборка.ЮрФизЛицо) Тогда
			СтруктураРеквизитов.Наименование = Выборка.ФИО;
		КонецЕсли;
		
		СтруктураРеквизитов.НаименованиеПолное = СтруктураРеквизитов.Наименование;
		СтруктураРеквизитов.Представление = СтруктураРеквизитов.НаименованиеПолное;
		
		СтруктураРеквизитов.АдресДоставки = ФормированиеПечатныхФорм.АдресДоставки(Выборка);	
	КонецЕсли;
	
	
	Возврат СтруктураРеквизитов;

КонецФункции 

// Преобразует номера телефонов в строку с номерами телефона без разделителей и "+"
Функция ПолучитьТелефоныДляПоиска(Телефон) Экспорт
	
	Если ПустаяСтрока(Телефон) Тогда
		ТелефонПоиск = "";
	Иначе	
		
		Телефоны = дт_МоиЗвонки.ПреобразоватьНомерТелефона(Телефон);
		
		Если ТипЗнч(Телефоны) = Тип("Массив") Тогда
			ТелефонПоиск = СтрСоединить(Телефоны, ",");
		Иначе
			ТелефонПоиск = Телефоны;
		КонецЕсли;
		
		ТелефонПоиск = СтрЗаменить(ТелефонПоиск, "+", "");
		
	КонецЕсли;	
	
	Возврат ТелефонПоиск;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти

#КонецЕсли