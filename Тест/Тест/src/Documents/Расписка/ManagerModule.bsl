
Процедура Печать(ТабДок, Ссылка) Экспорт
	
	Макет = Документы.Расписка.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Расписка.АдресРегистрации,
	|	Расписка.АдресРегистрацииПлательщика,
	|	Расписка.Выдан,
	|	Расписка.ВыданПлательщика,
	|	Расписка.Дата,
	|	Расписка.ДатаВыдачи,
	|	Расписка.ДатаВыдачиПлательщика,
	|	Расписка.КодПодразделения,
	|	Расписка.КодПодразделенияПлательщика,
	|	Расписка.Номер,
	|	Расписка.ПереданнаяСумма,
	|	Расписка.Плательщик,
	|	Расписка.СерияИНомер,
	|	Расписка.СерияИНомерПлательщика,
	|	Расписка.Сотрудник,
	|	Расписка.Город,
	|	Расписка.НомерПродажи
	|ИЗ
	|	Документ.Расписка КАК Расписка
	|ГДЕ
	|	Расписка.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Расписка = Макет.ПолучитьОбласть("Расписка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		Дата  = Формат(Выборка.Дата, "ДЛФ=DD");
		СуммаПрописью = ЧислоПрописью(Выборка.ПереданнаяСумма, , "рубль, рубля, рублей, м, копейка, копейки, копеек, ж");
		ДатаВыдачи = Формат(Выборка.ДатаВыдачи, "ДЛФ=D") + "г.";
		ДатаВыдачиПлательщика = Формат(Выборка.ДатаВыдачиПлательщика, "ДЛФ=D;") + "г.";
		Пол = "";
		ПолПлательщик = "";
		
		Если Прав(Выборка.Сотрудник, 2) = "ич" Тогда
			Пол = "зарегистрированный";
		Иначе
			Пол = "зарегистрированная";
		КонецЕсли;
		
		Если Прав(Выборка.Плательщик, 2) = "ич" Тогда
			ПолПлательщик = "зарегистрированного";
		Иначе
			ПолПлательщик = "зарегистрированной";
		КонецЕсли;
		
		Плательщик = ОбщегоНазначенияВызовСервера.ПросклонятьФИО(Выборка.Плательщик, 2 );
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ОбластьЗаголовок.Параметры.Город = "г." + Выборка.Город;
		ОбластьЗаголовок.Параметры.Дата = Дата;
		ТабДок.Вывести(ОбластьЗаголовок);

		Расписка.Параметры.Заполнить(Выборка);
		Расписка.Параметры.ПереданнаяСуммаПрописью = СуммаПрописью;
		Расписка.Параметры.ДатаВыдачи = ДатаВыдачи;
		Расписка.Параметры.ДатаВыдачиПлательщика = ДатаВыдачиПлательщика;
		Расписка.Параметры.Зарегистрирован = Пол;
		Расписка.Параметры.ЗарегистрированПлательщик = ПолПлательщик;
		Расписка.Параметры.Плательщик = Плательщик;
		ТабДок.Вывести(Расписка);
		
		Подвал.Параметры.Дата = Дата;
		Подвал.Параметры.Сотрудник = Выборка.Сотрудник;
		ТабДок.Вывести(Подвал);
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
	
КонецПроцедуры

