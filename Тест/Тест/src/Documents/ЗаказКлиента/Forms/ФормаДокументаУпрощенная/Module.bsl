&НаКлиенте
Перем Кэш;

#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Штрихкод = Получитькомпоненту();
	Элементы.QRКод.РазмерКартинки = РазмерКартинки.АвтоРазмер;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПродажаНайденная = ЕстьПродажаВЗаявке(Объект.Ссылка);
	Если Объект.ТипСделки = Перечисления.ТипСделки.Продажа И ПродажаНайденная
		= Документы.ПродажаЗапчастей.ПустаяСсылка() Тогда
		СоздатьПродажу();
	КонецЕсли;

	Если Объект.Продажа <> Документы.ПродажаЗапчастей.ПустаяСсылка() Тогда
		ОбновитьПродажу();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыДлительнойОперации = Новый Структура("Завершено, АдресРезультата, Идентификатор, Ошибка, ПодробноеПредставлениеОшибки");
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НайденаяПродажа = ЕстьПродажаВЗаявке(Объект.Ссылка);
	Иначе
		НайденаяПродажа = Документы.ПродажаЗапчастей.ПустаяСсылка();
	КонецЕсли;

	Если НайденаяПродажа <> Документы.ПродажаЗапчастей.ПустаяСсылка()  Тогда
		Объект.ТипСделки = Перечисления.ТипСделки.Продажа;
	Иначе
		Объект.ТипСделки = Перечисления.ТипСделки.Заявка;
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.НомерТелефона) И Объект.ИностранныйНомер = Истина Тогда
		Элементы.НомерТелефона.Маска = "";
	Иначе
		Элементы.НомерТелефона.Маска = "+7 ### ###-##-##";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Объект.Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	КлиентПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КлиентПриИзмененииНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Клиенты.Телефон
	|ИЗ
	|	Справочник.Клиенты КАК Клиенты
	|ГДЕ
	|	Клиенты.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", Объект.Клиент);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	ВыборкаДетальныеЗаписи.Следующий();
	Если Объект.НомерТелефона = "" Тогда
		Объект.НомерТелефона = ВыборкаДетальныеЗаписи.Телефон;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ТипОплатыПриИзменении(Элемент)
	ТипОплатыПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТипОплатыПриИзмененииНаСервере()
	Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипОплаты, "Организация");
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаПродажа Тогда
		ПолучитьДанныеПродажи();

	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаЗвонки Тогда

		ЗаданиеВыполнялосьРанее = ЗначениеЗаполнено(ПараметрыДлительнойОперации.Идентификатор);

		Если ЗаданиеВыполнялосьРанее Тогда
			Возврат;
		КонецЕсли
		;

		ЗаданиеВыполнено = ЗапуститьПолучениеЗвонковНаСервере();

		Если Не ЗаданиеВыполнено Тогда
			ПоказатьОповещениеПользователя("Получение списка звонков...");
			ПодключитьОбработчикОжидания("Подключаемый_ОжиданиеДлительнойОперации", 0.1, Истина);
		Иначе

			ЗагрузитьПолученныеЗвонки();

		КонецЕсли;
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаДоставка Тогда 
		ПолучитьДанныеДоставкиИзПродажи();
		УправлениеФормой();
		ВидимостьИНаименованиеРеквизитовКлиентаДоставка();		
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущийЭлемент = Элементы.ТоварыНоменклатура;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НайтиКлиета(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	ДопустимыеСимволы = "0123456789";
	Попытка
		КОбъект = Новый COMОбъект("htmlfile");
		ДанныеБуфера = КОбъект.ParentWindow.ClipboardData.Getdata("Text");
	Исключение
		ДанныеБуфера = "";
	КонецПопытки;
	ЛишниеСимволы = СтрСоединить(СтрРазделить(ДанныеБуфера, ДопустимыеСимволы));

	ИтоговаяСтрока = СтрСоединить(СтрРазделить(ДанныеБуфера, ЛишниеСимволы));

	Если СтрДлина(ИтоговаяСтрока) = 10 Тогда
		КОбъект.ParentWindow.ClipboardData.Setdata("Text", "");
		МассивСтрок = Новый Массив;
		Для Сч = 1 По СтрДлина(ИтоговаяСтрока) Цикл
			МассивСтрок.Добавить(Сред(ИтоговаяСтрока, Сч, 1));
		КонецЦикла;

		МассивСтрок[0] = "+7 " + МассивСтрок[0];
		МассивСтрок[3] = " " + МассивСтрок[3];
		МассивСтрок[6] = "-" + МассивСтрок[6];
		МассивСтрок[8] = "-" + МассивСтрок[8];
		НомерКлиента = СтрСоединить(МассивСтрок, "");
		Объект.НомерТелефона = НомерКлиента;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	СтараяЗаявка = ПроверкаЗаявки();
	Если СтараяЗаявка <> Неопределено Тогда

		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, СтараяЗаявка);

		ПоказатьВопрос(Оповещение, "Нажмите Да чтобы продолжить. Нажмите Нет чтобы открыть существующую заявку.",
			РежимДиалогаВопрос.ДаНетОтмена, 0, КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию

			"По данному номеру уже создана заявка. Вы хотите продолжить заполнение текущей?" // (необ.) заголовок
		);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИностранныйНомерПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.НомерТелефона) Или (Не ЗначениеЗаполнено(Объект.НомерТелефона)
		И Объект.ИностранныйНомер = Истина) Тогда
		Элементы.НомерТелефона.Маска = "";
	Иначе
		Элементы.НомерТелефона.Маска = "+7 ### ###-##-##";
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЕстьДоставкаПриИзменении(Элемент)
	Самовывоз = НЕ ЕстьДоставка;
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура СамовывозПриИзменении(Элемент)
	ЕстьДоставка = Не Самовывоз;
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура доставкаКлиентПриИзменении(Элемент)
		Элементы.ДанныеКлиентаДетали.Видимость = доставкаКлиент; 
КонецПроцедуры

&НаКлиенте
Процедура частныйПриИзменении(Элемент)
	ВидимостьИНаименованиеРеквизитовКлиентаДоставка();
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьИНаименованиеРеквизитовКлиентаДоставка()
	Если частный = Истина Тогда
		Элементы.Паспорт.Видимость = Истина;
		Элементы.Прописка.Видимость = Истина;
		Элементы.ИНН.Видимость = Ложь;
		Элементы.НаименованиеИлиФИО.Заголовок = "ФИО";
	Иначе
		Элементы.Паспорт.Видимость = Ложь;
		Элементы.Прописка.Видимость = Ложь;
		Элементы.ИНН.Видимость = Истина;
		Элементы.НаименованиеИлиФИО.Заголовок = "Наименование";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГородПолученияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ГородПолученияНачалоВыбораСервер();
КонецПроцедуры

&НаСервере
Процедура ГородПолученияНачалоВыбораСервер()
	Элементы.ГородПолучения.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	Если ЗначениеЗаполнено(РегионПолучения) Тогда
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "РегионПолучения");
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(НоваяСвязь);
		Элементы.ГородПолучения.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РегионПолученияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РегионПолученияНачалоВыбораНаСервере();
КонецПроцедуры

&НаСервере
Процедура РегионПолученияНачалоВыбораНаСервере()
	Элементы.РегионПолучения.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	Если ЗначениеЗаполнено(СтранаПолучения) Тогда
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "СтранаПолучения");
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(НоваяСвязь);
		Элементы.РегионПолучения.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);
	КонецЕсли;
КонецПроцедуры



&НаКлиенте
Процедура ГородПолученияПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(РегионПолучения) Тогда
		РегионПолучения = ПолучитьРегионГорода(); 
	КонецЕсли;	
	
	
	Если НЕ ЗначениеЗаполнено(СтранаПолучения) Тогда
		СтранаПолучения = ПолучитьСтрануРегиона(); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТрекНомерПриИзменении(Элемент)
	Результат = ПолучитьСсылкуДляОтслеживанияТовара(ТранспортнаяКомпания);
	СсылкаДляОтслеживанияТовара = Результат.СсылкаНаОтслеживание;
	ВставкаДляСсылкиОтслеживания = Результат.ВставкаДляСсылкиОтслеживания;
	Если СсылкаДляОтслеживанияТовара <> "" Тогда
		Если ТрекНомер <> "" И ВставкаДляСсылкиОтслеживания <> "" Тогда
			СсылкаНаОтслеживаниеДоставки = СсылкаДляОтслеживанияТовара + ВставкаДляСсылкиОтслеживания + ТрекНомер;
		Иначе
			СсылкаНаОтслеживаниеДоставки = СсылкаДляОтслеживанияТовара;
		КонецЕсли;
	Иначе
		СсылкаНаОтслеживаниеДоставки = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ТрекНомерПриИзмененииНаСервере()
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ТекДанные.Количество 	= 1;
	ТекДанные.Цена 		 	= ВозвратЦены(ТекДанные.Номенклатура);
	ТекДанные.Сумма 			= ТекДанные.Цена * ТекДанные.Количество;
КонецПроцедуры


&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ОбработкаИзмененияСтроки("Товары", "Цена");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПартияПриИзменении(Элемент)

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	ОткрытьФорму("ОбщаяФорма.СписокНоменклатурыЦенообразования");
КонецПроцедуры
&НаКлиенте
Процедура СоздатьКарточкуТовара(Команда)
//	ОповещениеОЗакрытииНового = Новый ОписаниеОповещения("ДобавлениеКарточки", ЭтаФорма);
//	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлементаПростая", , Элементы.Товары,
//		ЭтаФорма.УникальныйИдентификатор, , , ОповещениеОЗакрытииНового);

	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлементаПростая");
КонецПроцедуры

//Функция ДобавлениеКарточки(Ответ, ДополнительныеПараметры) Экспорт
//	Сообщить(Ответ);
//	Сообщить(ДополнительныеПараметры);
//КонецФункции

&НаКлиенте
Процедура ЗаполнитьИзФайла(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выбор файла";
	Диалог.Фильтр = "Excel файлы(*.xls;*.xlsx)|*.xls;*.xlsx";
	Диалог.ИндексФильтра = 0;
	Диалог.ПредварительныйПросмотр = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.МножественныйВыбор = Ложь; 
	//Диалог.ПолноеИмяФайла = ПутьКФайлу;

	Если Диалог.Выбрать() Тогда
		Попытка
			Эксель = Новый COMОбъект("Excel.Application");
			Эксель.DisplayAlerts = 0;
			Эксель.Visible = 0;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;

		ЭксельКнига = Эксель.Workbooks.Open(Диалог.ПолноеИмяФайла);
		КоличествоСтраниц = ЭксельКнига.Sheets.Count;
		
		// Перебираем все листы
		Для НомерЛиста = 1 По КоличествоСтраниц Цикл
			Лист = ЭксельКнига.Sheets(НомерЛиста);
			КоличествоСтрок = Лист.Cells(1, 1).SpecialCells(11).Row;
			КоличествоКолонок = Лист.Cells(1, 1).SpecialCells(11).Column;
			
			// Перебираем строки
			Для НомерСтроки = 2 По КоличествоСтрок Цикл
				артикулДетали = Строка(Лист.Cells(НомерСтроки, 1).Text);
				артикулДетали = СокрЛП(СтрЗаменить(артикулДетали, " ", ""));
				ЗаполнитьИзФайлаНаСервере(артикулДетали);

			КонецЦикла;
		КонецЦикла;

		Эксель.Workbooks.Close();
		Эксель.Application.Quit();

	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзФайлаНаСервере(артикулДетали)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Номенклатура.Ссылка КАК Номенклатура,
				   |	Номенклатура.РекомендованаяЦена КАК Цена
				   |ИЗ
				   |	Справочник.Номенклатура КАК Номенклатура
				   |ГДЕ
				   |	Номенклатура.Артикул = &Артикул";

	Запрос.УстановитьПараметр("Артикул", артикулДетали);
	ВыборкаДеталей = Запрос.Выполнить().Выбрать();

	Если ВыборкаДеталей.Количество() = 0 Тогда
		Сообщить("Товар с артикулом " + артикулДетали + " не найден!");
	КонецЕсли;

	Если ВыборкаДеталей.Количество() > 1 Тогда
		Сообщить("Товар с артикулом " + артикулДетали + " найден в количестве " + ВыборкаДеталей.Количество() + " шт.");
	КонецЕсли;

	Пока ВыборкаДеталей.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.Номенклатура = ВыборкаДеталей.Номенклатура;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Цена = ВыборкаДеталей.Цена;
		НоваяСтрока.Сумма = ВыборкаДеталей.Цена;
	КонецЦикла;
КонецПроцедуры
&НаКлиенте
Процедура НарядНаСборкуКод(Команда)
	Если ЗначениеЗаполнено(Объект.Продажа) Тогда
		ПараметрыФормы = Новый Структура("Ключ", Объект.Продажа);
		ФормаПродажи = ПолучитьФорму("Документ.ПродажаЗапчастей.Форма.ФормаДокумента", ПараметрыФормы);
		ТабДокумент  = ФормаПродажи.НарядНаСборкуСервер("Код");
		ТабДокумент.Показать();
	Иначе
		Сообщить("Продажа не создана.");
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура НарядНаВыдачу(Команда)
	Если ЗначениеЗаполнено(Объект.Продажа) Тогда
		ПараметрыФормы = Новый Структура("Ключ", Объект.Продажа);
		ФормаПродажи = ПолучитьФорму("Документ.ПродажаЗапчастей.Форма.ФормаДокумента", ПараметрыФормы);
		ФормаПродажи.ПечатьСписания();
	Иначе
		Сообщить("Продажа не создана.");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НайтиКлиета(Команда)
	ПоискКлиента();
КонецПроцедуры

&НаКлиенте
Процедура ПрослушатьЗаписьЗвонка(Команда)
	Если Элементы.ТаблицаЗвонки.ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Если Кэш = Неопределено Тогда
		Кэш = Новый Структура();
		Кэш.Вставить("ФайлыЗвонков", Новый Соответствие());
	КонецЕсли;
	
	СсылкаНаФайл = Элементы.ТаблицаЗвонки.ТекущиеДанные.ЗаписьURL;
	СсылкаНаФайлЛокальная = Кэш.ФайлыЗвонков.Получить(СсылкаНаФайл);
	
	Если СсылкаНаФайлЛокальная = Неопределено Тогда
		
		СсылкаНаФайлЛокальная = дт_МоиЗвонкиКлиент.ПолучитьФайлЗаписи(СсылкаНаФайл);
		Если СсылкаНаФайлЛокальная = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		Кэш.ФайлыЗвонков.Вставить(СсылкаНаФайл, СсылкаНаФайлЛокальная);
		
	КонецЕсли;
	дт_МоиЗвонкиКлиент.ПрослушатьФайл(СсылкаНаФайлЛокальная);	  
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаЗаявку(Команда)
	дт_ОбщегоНазначенияКлиентСервер.ПоместитьТексВБуферОбмена("https://sklad.wt10-crm.ru/application-details/" + Объект.Номер);
	Сообщить("Ссылна на заявку склада скопирована.");
КонецПроцедуры


&НаКлиенте
Процедура ПерейтиВЗаявкуWT10(Команда)
	Номер = объект.Номер; 
	ОбщДействия.ОткрытьЗаявкуНаСайтеWT10(Номер); 
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГород(Команда)
	ОткрытьФорму("Справочник.Город.ФормаОбъекта");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДетальНаWT10(Команда)
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ОбщДействия.ОткрытьКарточкуТовараНаСайтеWT10(ПолучитьКодНоменклатуры(ТекДанные.Номенклатура));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДетальНаZapchasti10(Команда)
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекДанные.Партия) Тогда
			ОбщДействия.ОткрытьДетальНаСайтеZapchasti10(ТекДанные.Партия);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НарядНаДоставку(Команда)
	Если ЗначениеЗаполнено(Объект.Продажа) Тогда
		ТабДокумент = дт_Продажи.НарядДоставка(Объект.Продажа);
		ТабДокумент.Показать();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьПродажу()
	ОбъектПродажа = Объект.Продажа.ПолучитьОбъект();
	ОбъектПродажа.Организация = Объект.Организация;
	ОбъектПродажа.Клиент = Объект.Клиент;
	ОбъектПродажа.КтоПродал = Объект.Ответственный;
	ОбъектПродажа.ОжидаемаяДатаВыплаты = ДатаОплаты;
	// Обновить данные доставки
	ОбъектПродажа.ЕстьДоставка = ЕстьДоставка;
	ОбъектПродажа.Самовывоз = Самовывоз;
	ОбъектПродажа.ТранспортнаяКомпания = ТранспортнаяКомпания;
	ОбъектПродажа.ЕстьТрекНомер = ЕстьТрекНомер;
	ОбъектПродажа.ТрекНомер = ТрекНомер;
	ОбъектПродажа.СсылкаНаОтслеживаниеДоставки = СсылкаНаОтслеживаниеДоставки;
	ОбъектПродажа.Вес = Вес;
	ОбъектПродажа.ГородОтправки = ГородОтправки;
	ОбъектПродажа.СтранаПолучения = СтранаПолучения;
	ОбъектПродажа.РегионПолучения = РегионПолучения;
	ОбъектПродажа.ГородПолучения = ГородПолучения;
	ОбъектПродажа.СтоимостьДоставки = СтоимостьДоставки;
	ОбъектПродажа.КоммДост = КоммДост;
	ОбъектПродажа.Водитель = Водитель;
	ОбъектПродажа.СтатусЗаказаВТК = СтатусЗаказаВТК;
	ОбъектПродажа.ДатаПолучения = ДатаПолучения;
	ОбъектПродажа.СтатусДоставки = СтатусДоставки;
	ОбъектПродажа.ИсторияОтслеживанияЗаказа = ИсторияОтслеживанияЗаказа;
	ОбъектПродажа.доставкаКлиент = доставкаКлиент;
	ОбъектПродажа.Частный = Частный;
	ОбъектПродажа.НаименованиеИлиФИО = НаименованиеИлиФИО;
	ОбъектПродажа.Паспорт = Паспорт;
	ОбъектПродажа.Прописка = Прописка;
	ОбъектПродажа.ИНН = ИНН;
	ОбъектПродажа.Телефон = Телефон;
	
	ОбъектПродажа.Таблица.Очистить();
	Для Каждого СтрокаТЧЗаявки Из Объект.Товары Цикл
		НоваяСтрока = ОбъектПродажа.Таблица.Добавить();
		НоваяСтрока.Партия = СтрокаТЧЗаявки.Партия;
		НоваяСтрока.Товар = СтрокаТЧЗаявки.Номенклатура;
		Если ЗначениеЗаполнено(СтрокаТЧЗаявки.Партия) Тогда
			НоваяСтрока.Склад = ПолучитьСкладПоПартии(СтрокаТЧЗаявки.Партия);
		Иначе
			НоваяСтрока.Склад = Справочники.Склады.НайтиПоКоду("000000002");
		КонецЕсли;
		НоваяСтрока.МестоХранения = ПолучитьАдрес(СтрокаТЧЗаявки.Номенклатура, НоваяСтрока.Склад, СтрокаТЧЗаявки.Партия);
		НоваяСтрока.Количество = СтрокаТЧЗаявки.Количество;
		НоваяСтрока.Цена = СтрокаТЧЗаявки.Цена;
		НоваяСтрока.СуммаНДС = СтрокаТЧЗаявки.СуммаНДС;
		НоваяСтрока.Сумма = СтрокаТЧЗаявки.Сумма;
	КонецЦикла;
	ОбъектПродажа.Записать(РежимЗаписиДокумента.Проведение);
КонецПроцедуры

&НаСервере
Процедура СоздатьПродажу()
	НовыйДокумент = Документы.ПродажаЗапчастей.СоздатьДокумент();
	НовыйДокумент.Дата = ТекущаяДатаСеанса();
	НовыйДокумент.Клиент = Объект.Клиент;
	НовыйДокумент.ЗаказКлиента = Объект.Ссылка;
	НовыйДокумент.СтатусДоставки = Перечисления.СтатусОтправки.Отправлен;
	НовыйДокумент.ОжидаемаяДатаВыплаты = ТекущаяДата();
	НовыйДокумент.Организация = Объект.Организация;
	НовыйДокумент.КтоПродал = Объект.Ответственный;
	НовыйДокумент.Самовывоз = Истина;

	Для Каждого СтрокаТЧЗаявки Из Объект.Товары Цикл
		НоваяСтрока = НовыйДокумент.Таблица.Добавить();
		НоваяСтрока.Партия = СтрокаТЧЗаявки.Партия;
		НоваяСтрока.Товар = СтрокаТЧЗаявки.Номенклатура;
		Если ЗначениеЗаполнено(СтрокаТЧЗаявки.Партия) Тогда
			НоваяСтрока.Склад = ПолучитьСкладПоПартии(СтрокаТЧЗаявки.Партия);
		Иначе
			НоваяСтрока.Склад = Справочники.Склады.НайтиПоКоду("000000002");
		КонецЕсли;
		НоваяСтрока.МестоХранения = ПолучитьАдрес(СтрокаТЧЗаявки.Номенклатура, НоваяСтрока.Склад, СтрокаТЧЗаявки.Партия);
		НоваяСтрока.Количество = СтрокаТЧЗаявки.Количество;
		НоваяСтрока.Цена = СтрокаТЧЗаявки.Цена;
		НоваяСтрока.СуммаНДС = СтрокаТЧЗаявки.СуммаНДС;
		НоваяСтрока.Сумма = СтрокаТЧЗаявки.Сумма;
	КонецЦикла;
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	Объект.Продажа = ЕстьПродажаВЗаявке(Объект.Ссылка);
КонецПроцедуры


&НаСервере
Процедура ПолучитьДанныеПродажи()
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НайденаяПродажа = ЕстьПродажаВЗаявке(Объект.Ссылка);
	Иначе
		НайденаяПродажа = Документы.ПродажаЗапчастей.ПустаяСсылка();
	КонецЕсли;

	Если НайденаяПродажа <> Документы.ПродажаЗапчастей.ПустаяСсылка() Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПродажаЗапчастей.Ссылка,
		|	ПродажаЗапчастей.ОжидаемаяДатаВыплаты
		|ИЗ
		|	Документ.ПродажаЗапчастей КАК ПродажаЗапчастей
		|ГДЕ
		|	ПродажаЗапчастей.Ссылка = &НайденаяПродажа";

		Запрос.УстановитьПараметр("НайденаяПродажа", НайденаяПродажа);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл
			Объект.Продажа = НайденаяПродажа;
			ДатаОплаты = Выборка.ОжидаемаяДатаВыплаты;
		КонецЦикла;
	КонецЕсли;
	Записать();
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеДоставкиИзПродажи()
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НайденаяПродажа = ЕстьПродажаВЗаявке(Объект.Ссылка);
	Иначе
		НайденаяПродажа = Документы.ПродажаЗапчастей.ПустаяСсылка();
	КонецЕсли;

	Если НайденаяПродажа <> Документы.ПродажаЗапчастей.ПустаяСсылка() Тогда
		// Все данные о для доставки
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПродажаЗапчастей.ЕстьДоставка,
			|	ПродажаЗапчастей.Самовывоз,
			|	ПродажаЗапчастей.ТранспортнаяКомпания,
			|	ПродажаЗапчастей.ЕстьТрекНомер,
			|	ПродажаЗапчастей.ТрекНомер,
			|	ПродажаЗапчастей.СсылкаНаОтслеживаниеДоставки,
			|	ПродажаЗапчастей.Вес,
			|	ПродажаЗапчастей.ГородОтправки,
			|	ПродажаЗапчастей.СтранаПолучения,
			|	ПродажаЗапчастей.РегионПолучения,
			|	ПродажаЗапчастей.ГородПолучения,
			|	ПродажаЗапчастей.СтоимостьДоставки,
			|	ПродажаЗапчастей.КоммДост,
			|	ПродажаЗапчастей.Водитель,
			|	ПродажаЗапчастей.СтатусЗаказаВТК,
			|	ПродажаЗапчастей.ДатаПолучения,
			|	ПродажаЗапчастей.СтатусДоставки,
			|	ПродажаЗапчастей.ИсторияОтслеживанияЗаказа,
			|	ПродажаЗапчастей.доставкаКлиент,
			|	ПродажаЗапчастей.частный,
			|	ПродажаЗапчастей.НаименованиеИлиФИО,
			|	ПродажаЗапчастей.Паспорт,
			|	ПродажаЗапчастей.Прописка,
			|	ПродажаЗапчастей.ИНН,
			|	ПродажаЗапчастей.Телефон
			|ИЗ
			|	Документ.ПродажаЗапчастей КАК ПродажаЗапчастей
			|ГДЕ
			|	ПродажаЗапчастей.Ссылка = &НайденаяПродажа";
		
		Запрос.УстановитьПараметр("НайденаяПродажа", НайденаяПродажа);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЕстьДоставка = Выборка.ЕстьДоставка;
			Самовывоз = Выборка.Самовывоз;
			ТранспортнаяКомпания = Выборка.ТранспортнаяКомпания;
			ЕстьТрекНомер = Выборка.ЕстьТрекНомер;
			ТрекНомер = Выборка.ТрекНомер;
			СсылкаНаОтслеживаниеДоставки = Выборка.СсылкаНаОтслеживаниеДоставки;
			Вес = Выборка.Вес;
			ГородОтправки = Выборка.ГородОтправки;
			СтранаПолучения = Выборка.СтранаПолучения;
			РегионПолучения = Выборка.РегионПолучения;
			ГородПолучения = Выборка.ГородПолучения;
			СтоимостьДоставки = Выборка.СтоимостьДоставки;
			КоммДост = Выборка.КоммДост;
			Водитель = Выборка.Водитель;
			СтатусЗаказаВТК = Выборка.СтатусЗаказаВТК;
			ДатаПолучения = Выборка.ДатаПолучения;
			СтатусДоставки = Выборка.СтатусДоставки;
			ИсторияОтслеживанияЗаказа = Выборка.ИсторияОтслеживанияЗаказа;
			доставкаКлиент = Выборка.доставкаКлиент;
			Частный = Выборка.Частный;
			НаименованиеИлиФИО = Выборка.НаименованиеИлиФИО;
			Паспорт = Выборка.Паспорт;
			Прописка = Выборка.Прописка;
			ИНН = Выборка.ИНН;
			Телефон = Выборка.Телефон;
		КонецЦикла;
		
	КонецЕсли;
	Записать();
КонецПроцедуры

&НаСервере
Функция Получитькомпоненту()

	ТекстОшибки = "";

	Штрихкод =  ГенераторШтрихКода.ПолучитьКомпонентуШтрихКодирования(ТекстОшибки);
	Штрихкод.Ширина = 300;
	Штрихкод.Высота = 300;
	Штрихкод.ТипКода = 16;
	Штрихкод.УголПоворота = 0;
	Штрихкод.ЗначениеКода = Объект.Номер;
	Штрихкод.ПрозрачныйФон = Истина;
	Штрихкод.ОтображатьТекст = Ложь;

	ДвоичныйШтрихКод = штрихкод.ПолучитьШтрихКод();
	КартинкаШтрихКод = Новый Картинка(ДвоичныйШтрихКод, Истина);

	QRкод = ПоместитьВоВременноеХранилище(КартинкаШтрихКод, УникальныйИдентификатор);
	Возврат Истина;

КонецФункции

&НаКлиенте
Процедура ДобавлениеКлиента(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ПараметрыОткрытия = Новый Структура;

	ПараметрыОткрытия.Вставить("НомерТелефона", Объект.НомерТелефона);
	ПараметрыОткрытия.Вставить("КлиентНаименование", Объект.КлиентНаименование);
	ПараметрыОткрытия.Вставить("Ответственный", ПользователиКлиентСервер.ТекущийПользователь());

	ОповещениеОЗакрытииНового = Новый ОписаниеОповещения("НайтиКлиентанового_Завершение", ЭтаФорма);
	ОткрытьФорму("Справочник.Клиенты.Форма.ФормаЭлемента", ПараметрыОткрытия, ЭтаФорма,
		ЭтаФорма.УникальныйИдентификатор, , , ОповещениеОЗакрытииНового);

КонецПроцедуры

&НаКлиенте
Процедура ПоискКлиента()
	Клиенты = НайтиКлиентаНаСервере(Объект.НомерТелефона);

	Если Клиенты = Неопределено Тогда
		ПоказатьОповещениеПользователя("Не найдено");
		Оповещение = Новый ОписаниеОповещения("ДобавлениеКлиента", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Добавить нового клиента?", РежимДиалогаВопрос.ДаНет);
		//ВыполнитьОбработкуОповещения(Оповещение,); 
	ИначеЕсли Клиенты.Количество() > 1 Тогда

		ПоказатьОповещениеПользователя(СтрШаблон("Найдено: %1", Клиенты.Количество()));

		ОповещениеОЗакрытии = Новый ОписаниеОповещения("НайтиКлиента_Завершение", ЭтаФорма);
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Отбор", Новый Структура("Ссылка", Клиенты));
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытия.Вставить("ТекущаяСтрока", Объект.Клиент);

		ОткрытьФорму("Справочник.Клиенты.Форма.ФормаВыбора", ПараметрыОткрытия, ЭтаФорма,
			ЭтаФорма.УникальныйИдентификатор, , , ОповещениеОЗакрытии);

	Иначе
		Если Объект.Клиент = Клиенты[0] Тогда
			ПоказатьОповещениеПользователя("Найдено", , "Обновление не требуется");
		Иначе
			Объект.Клиент = Клиенты[0];
			Объект.КлиентНаименование = Строка(Клиенты[0]);
			ПоказатьОповещениеПользователя("Найдено", , "Обновлено");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиКлиентаНаСервере(Знач НомерТелефона)

	Результат = Неопределено;

	Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
		Возврат Результат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Клиенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Клиенты КАК Клиенты
	|ГДЕ
	|	НЕ Клиенты.ПометкаУдаления
	|	И (Клиенты.Телефон = &НомерТелефона
	|			ИЛИ Клиенты.ДополнительныеКонтакты.Телефон = &НомерТелефона)";

	Запрос.УстановитьПараметр("НомерТелефона", НомерТелефона);
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;

	Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	Возврат Результат;

КонецФункции
&НаКлиенте
Процедура ОбработкаИзмененияСтроки(ИмяТабличнойЧасти, Поле = Неопределено, ДанныеСтроки = Неопределено)

	ТекДанные = ?(ДанныеСтроки = Неопределено, Элементы[ИмяТабличнойЧасти].ТекущиеДанные, ДанныеСтроки);

	Если Поле = "Сумма" Тогда
		Если ТекДанные.Количество <> 0 Тогда
			ТекДанные.Цена = ТекДанные.Сумма / ТекДанные.Количество;
		КонецЕсли;
	Иначе
		ТекДанные.Сумма = ТекДанные.Цена * ТекДанные.Количество;
		ТекДанные.СуммаНДС = 0;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдрес(Номенклатура, Склад, ИндКод)
	Запрос  = Новый запрос;
	Запрос.текст = "ВЫБРАТЬ
				   |	дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения КАК МестоХранения,
				   |	ЕСТЬNULL(Стеллажик.ГруппаМестХранения, ЗНАЧЕНИЕ(Справочник.ГруппыМестХранения.ПустаяСсылка)) КАК ГруппаМестХранения,
				   |	ВЫБОР
				   |		КОГДА ИндНомер.Стеллаж <> ЗНАЧЕНИЕ(Справочник.стеллаж.пустаяссылка)
				   |			ТОГДА ИндНомер.Стеллаж
				   |		ИНАЧЕ Стеллажик.Ссылка
				   |	КОНЕЦ КАК Стеллаж
				   |ИЗ
				   |	РегистрСведений.дт_МестаХраненияНоменклатуры.СрезПоследних(
				   |			&ДатаСреза,
				   |			Склад = &Склад
				   |				И Номенклатура = &Номенклатура) КАК дт_МестаХраненияНоменклатурыСрезПоследних
				   |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Стеллаж КАК Стеллажик
				   |		ПО дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения = Стеллажик.Ссылка
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИндНомер КАК ИндНомер
				   |		ПО дт_МестаХраненияНоменклатурыСрезПоследних.Номенклатура = ИндНомер.индкод.Владелец
				   |ГДЕ
				   |	ИндНомер.индкод = &индкод";

	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("индкод", ИндКод);
	Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДата());

	Выборка = Запрос.Выполнить().Выбрать();

	Если выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Выборка.стеллаж;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьПродажаВЗаявке(СсылкаНаЗаявку)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПродажаЗапчастей.Ссылка КАК Продажа
	|ИЗ
	|	Документ.ПродажаЗапчастей КАК ПродажаЗапчастей
	|ГДЕ
	|	ПродажаЗапчастей.ЗаказКлиента = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", СсылкаНаЗаявку);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Документы.ПродажаЗапчастей.ПустаяСсылка();
	КонецЕсли;
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Возврат ВыборкаДетальныеЗаписи.Продажа;
	КонецЦикла;

КонецФункции

&НаКлиенте
Процедура НайтиКлиентанового_Завершение(П1, П2) Экспорт
	ПоискКлиента();
КонецПроцедуры

&НаКлиенте
Процедура НайтиКлиента_Завершение(П1, П2) Экспорт
	
	Если П1 <> Неопределено Тогда
		Объект.Клиент = П1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверкаЗаявки()
	Запрос  = Новый запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказКлиента.Ссылка КАК Ссылка,
	|	ЗаказКлиента.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	(ЗаказКлиента.Состояние = ЗНАЧЕНИЕ(Перечисление.дт_СостоянияЗаказовКлиента.Ожидание)
	|			ИЛИ ЗаказКлиента.Состояние = ЗНАЧЕНИЕ(Перечисление.дт_СостоянияЗаказовКлиента.Думает))
	|	И ЗаказКлиента.НомерТелефона = &НомерТелефона
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказКлиента.Дата УБЫВ";
	Запрос.УстановитьПараметр("НомерТелефона", объект.НомерТелефона);

	Выборка = Запрос.Выполнить().Выбрать();

	Если выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Выборка.ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ПоказатьПредупреждениеПервомуМенеджеру(Параметры);
	КонецЕсли;

	Если Результат = КодВозвратаДиалога.Нет Тогда
		ПараметрыФормы = Новый Структура("Ключ", Параметры);
		ОткрытьФорму("Документ.ЗаказКлиента.Форма.ФормаДокумента", ПараметрыФормы);
		ЭтаФорма.Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПоказатьПредупреждениеПервомуМенеджеру(СтараяЗаявка)
	Если Не ЗначениеЗаполнено(СтараяЗаявка.Ответственный) Тогда
		Возврат;
	КонецЕсли;
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	НапоминаниеПользователю = РегистрыСведений.НапоминанияПользователя.СоздатьМенеджерЗаписи();
	НапоминаниеПользователю.Пользователь = СтараяЗаявка.Ответственный;
	НапоминаниеПользователю.ВремяСобытия = ТекущаяДата() + 20;
	НапоминаниеПользователю.СрокНапоминания = ТекущаяДата() + 25;
	НапоминаниеПользователю.Описание = "" + ТекущийПользователь.ПолноеИмя + " создал заявку с номером клиента "
		+ СтараяЗаявка.НомерТелефона;
	НапоминаниеПользователю.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя;
	НапоминаниеПользователю.ИнтервалВремениНапоминания = 120;
	НапоминаниеПользователю.Записать();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСкладПоПартии(индкод)
	
	///+ГомзМА 26.09.2023
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрНакопления1Остатки.Товар КАК Товар,
	|	РегистрНакопления1Остатки.индкод КАК индкод,
	|	РегистрНакопления1Остатки.Склад КАК Склад
	|ИЗ
	|	РегистрНакопления.РегистрНакопления1.Остатки КАК РегистрНакопления1Остатки
	|ГДЕ
	|	РегистрНакопления1Остатки.индкод = &индкод";

	Запрос.УстановитьПараметр("индкод", индкод);

	РезультатЗапроса = Запрос.Выполнить().Выбрать();

	РезультатЗапроса.Следующий();

	Возврат РезультатЗапроса.Склад;
	///-ГомзМА 26.09.2023

КонецФункции

&НаСервере
Функция ЗапуститьПолучениеЗвонковНаСервере()

	Если Не ПолучитьФункциональнуюОпцию("дт_ИспользоватьИнтеграциюМоиЗвонки") Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда

		Возврат Истина;

	КонецЕсли;

	Если Не ПараметрыДлительнойОперации.Идентификатор = Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ПараметрыДлительнойОперации.Идентификатор);
	КонецЕсли;
	ПараметрыДлительнойОперации.Идентификатор   = Неопределено;
	ПараметрыДлительнойОперации.Завершено       = Истина;
	ПараметрыДлительнойОперации.АдресРезультата = Неопределено;
	ПараметрыДлительнойОперации.Ошибка          = Неопределено;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Ссылка", Объект.Ссылка);

	НаименованиеМетода = НСтр("ru = 'Заполнение списка звонков'");

	Попытка

		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор, "дт_МоиЗвонки.ЗапуститьПолучениеТаблицыЗвонковПоЗаявке", ПараметрыМетода,
			НаименованиеМетода);
	Исключение

		ПараметрыДлительнойОперации.Ошибка = ОписаниеОшибки();

		Возврат Ложь;
	КонецПопытки;
	ПараметрыДлительнойОперации.Идентификатор   = РезультатВыполнения.ИдентификаторЗадания;
	ПараметрыДлительнойОперации.Завершено       = РезультатВыполнения.ЗаданиеВыполнено;
	ПараметрыДлительнойОперации.АдресРезультата = РезультатВыполнения.АдресХранилища;

	Возврат РезультатВыполнения.ЗаданиеВыполнено;

КонецФункции

&НаКлиенте
Процедура ЗагрузитьПолученныеЗвонки()
	
	ЗагрузитьПолученныеЗвонкиСервер();
	ПоказатьОповещениеПользователя("Список звонков получен");
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПолученныеЗвонкиСервер()

	ТаблицаЗвонки.Очистить();

	Если ПараметрыДлительнойОперации.АдресРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли
	;
	//
	РезультатВыполнения = ПолучитьИзВременногоХранилища(ПараметрыДлительнойОперации.АдресРезультата);

	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда

		ТаблицаРезультат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "ТаблицаРезультат");
		Если ТаблицаРезультат <> Неопределено Тогда

			Сч = 0;
			ТаблицаЗвонки.Загрузить(ТаблицаРезультат);
			Пока (Сч < ТаблицаЗвонки.Количество()) Цикл
				Строка = ТаблицаЗвонки.Получить(Сч);
				Если Строка.Дата < НачалоДня(Объект.Дата) - (3600 * 24 * 2) Или Строка.Дата
					> объект.ДатаИзмененияСостояния + (24 * 3600 * 5) Тогда
					ТаблицаЗвонки.Удалить(Сч);
				Иначе
					Сч = Сч + 1;
				КонецЕсли;
			КонецЦикла;

			ТаблицаЗвонки.Сортировать("Дата Убыв");
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВозвратЦены(Товар)
	Запрос = Новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Номенклатура.РекомендованаяЦена КАК РекомендованаяЦена
				   |ИЗ
				   |	Справочник.Номенклатура КАК Номенклатура
				   |ГДЕ
				   |	Номенклатура.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("ссылка", товар);

	Выборка = запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.РекомендованаяЦена;
КонецФункции

&НаКлиенте
Процедура УправлениеФормой()
	Элементы.ДанныеОДоставкеОбщие.Видимость = ЕстьДоставка;
	Элементы.ДанныеКлиентаДетали.Видимость = доставкаКлиент;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКодНоменклатуры(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Код
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";

	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Выборка.Следующий();
	Возврат Выборка.Код;
КонецФункции

&НаСервере
Функция ПолучитьРегионГорода()
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Город.Владелец
		|ИЗ
		|	Справочник.Город КАК Город
		|ГДЕ
		|	Город.Ссылка = &Город";

		Запрос.УстановитьПараметр("Город", ГородПолучения);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Выборка.Следующий();

		Возврат Выборка.Владелец;
	Исключение
		Возврат Справочники.Область.ПустаяСсылка();
	КонецПопытки;
КонецФункции 

&НаСервере
Функция ПолучитьСтрануРегиона()
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Область.Владелец
		|ИЗ
		|	Справочник.Область КАК Область
		|ГДЕ
		|	Область.Ссылка = &Регион";

		Запрос.УстановитьПараметр("Регион", РегионПолучения);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Выборка.Следующий();
		Возврат Выборка.Владелец;
	Исключение
		Возврат Справочники.Страна.ПустаяСсылка();
	КонецПопытки;
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСсылкуДляОтслеживанияТовара(ТранспортнаяКомпания)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспротнаяКомпания.СсылкаНаОтслеживание КАК СсылкаНаОтслеживание,
	|	ТранспротнаяКомпания.ВставкаДляСсылкиОтслеживания КАК ВставкаДляСсылкиОтслеживания
	|ИЗ
	|	Справочник.ТранспротнаяКомпания КАК ТранспротнаяКомпания
	|ГДЕ
	|	ТранспротнаяКомпания.Ссылка = &ТК";

	Запрос.УстановитьПараметр("ТК", ТранспортнаяКомпания);

	Выборка = Запрос.Выполнить().Выбрать();

	Выборка.Следующий();
	ДанныеДляОтслеживания = Новый Структура("СсылкаНаОтслеживание,ВставкаДляСсылкиОтслеживания",
		Выборка.СсылкаНаОтслеживание, Выборка.ВставкаДляСсылкиОтслеживания);
	Возврат ДанныеДляОтслеживания;
КонецФункции
#КонецОбласти