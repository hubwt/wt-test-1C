
Функция ЕстьОтрицательныеОстатки(Ссылка, Дата, Склад) Экспорт
	
	///+ГомзМА 05.04.2023
	Результат = Ложь;
	Граница = Новый Граница(Дата, ВидГраницы.Включая);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДвижениеТМЦОстатки.ТМЦ КАК ТМЦ,
		|	ДвижениеТМЦОстатки.МестоХранения КАК МестоХранения,
		|	ДвижениеТМЦОстатки.Код КАК Код,
		|	ДвижениеТМЦОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ДвижениеТМЦ.Остатки(
		|			&Дата,
		|			МестоХранения = &Склад
		|				И (ТМЦ, Код) В
		|					(ВЫБРАТЬ
		|						СписаниеТМЦСписокТМЦ.ТМЦ КАК ТМЦ,
		|						СписаниеТМЦСписокТМЦ.Код КАК Код
		|					ИЗ
		|						Документ.СписаниеТМЦ.СписокТМЦ КАК СписаниеТМЦСписокТМЦ
		|					ГДЕ
		|						СписаниеТМЦСписокТМЦ.Ссылка.Ссылка = &Ссылка)) КАК ДвижениеТМЦОстатки
		|ГДЕ
		|	ДвижениеТМЦОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Дата", Граница);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаВывода = СтрШаблон("Не хватает товара %1 на складе: ""%2"", в количестве %3", Выборка.ТМЦ, Выборка.МестоХранения, -Выборка.КоличествоОстаток);
			Сообщить(СтрокаВывода);
		КонецЦикла;
		Результат = Истина;
	КонецЕсли;	
	
	Возврат Результат;
	///-ГомзМА 05.04.2023 
	
КонецФункции //ЕстьОтрицательныеОстатки(Ссылка, Дата, Склад)