
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс



#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеЗапчастей")	Тогда
		ИмяТЧ = "Таблица";
		ИмяРеквизитаСклад = "Склад";
		ИмяРеквизитаНоменклатура = "Товар";
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ИмяТЧ = "Товары";
		ИмяРеквизитаСклад = "СкладПолучатель";
		ИмяРеквизитаНоменклатура = "Товар";
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаНаСборку") Тогда
		ИмяТЧ = "Товары";
		ИмяРеквизитаСклад = "Склад";
		ИмяРеквизитаНоменклатура = "Номенклатура";
	Иначе 
		Возврат
	КонецЕсли;
	
	ВидДокумента = ДанныеЗаполнения.Метаданные().Имя;
	
	Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, ИмяРеквизитаСклад);
	Основание = ДанныеЗаполнения;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
		|	ТабличнаяЧасть." + ИмяРеквизитаНоменклатура + " КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ." + ВидДокумента + "." + ИмяТЧ + " КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;  
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	МестаХранения.МестоХранения КАК МестоХраненияИсточник
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.дт_МестаХраненияНоменклатуры.СрезПоследних(&Дата, Склад = &Склад) КАК МестаХранения
		|		ПО ВТ_Товары.Номенклатура = МестаХранения.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата()) - 1);
	
	РезультатЗапроса = Запрос.Выполнить();
	

	Товары.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ДополнительныеСвойства.Вставить("ЭтоНовый",                    ЭтоНовый()); 
	ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь); 
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	
	ПараметрыПроведения = Документы.УстановкаМестХранения.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	
	// Формирование движений
	дт_АдресноеХранение.ОтразитьДвижения(ПараметрыПроведения, Движения, Отказ);
	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти

#КонецЕсли