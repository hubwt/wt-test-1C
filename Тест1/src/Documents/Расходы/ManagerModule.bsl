#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьСуммыВыплатПоОснованию(Основание) Экспорт

	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.Расходы) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(Расходы.Сумма) КАК Сумма,
		|	Расходы.Работник КАК Пользователь
		|ИЗ
		|	Документ.Расходы КАК Расходы
		|ГДЕ
		|	(Расходы.ВидОперации = &ВидОперации
		|	ИЛИ Расходы.Зарплата)
		|	И Расходы.Основание = &Основание
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Работник";
	
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ВидОперации", Перечисления.ВидыОперацийСписаниеДенежныхСредств.Зарплата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Возврат РезультатЗапроса.Выгрузить();
		
КонецФункции



#Область Проведение
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	НомераТаблиц = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст =
		ТекстЗапросаТопливоВБаках(НомераТаблиц)
		+ ТекстЗапросаСебестоимостьПеревозок(НомераТаблиц)
		+ ТекстЗапросаРасчетыСПоставщиками(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		Таблица =  Результат[НомерТаблицы.Значение].Выгрузить();
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Таблица);
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции


Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.ВидРасхода КАК ВидРасхода,
	|	Реквизиты.Сумма КАК Сумма,
	|	Реквизиты.Автомобиль КАК Автомобиль,
	|	Реквизиты.МаршрутныйЛист КАК МаршрутныйЛист,
	|	Реквизиты.КоличествоТопливо КАК КоличествоТопливо,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Основание КАК Основание
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.Расходы КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.ВидРасхода КАК ВидРасхода,
	|	Реквизиты.Сумма КАК Сумма,
	|	Реквизиты.Автомобиль КАК Автомобиль,
	|	Реквизиты.МаршрутныйЛист КАК МаршрутныйЛист,
	|	Реквизиты.КоличествоТопливо КАК КоличествоТопливо,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Основание КАК Основание
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса + дт_ОбщегоНазначенияВызовСервераПовтИсп.ТекстРазделителяЗапросовПакета();
	

КонецФункции // ТекстЗапросаРеквизиты()


Функция ТекстЗапросаТопливоВБаках(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаТопливоВБаках", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Автомобиль КАК Автомобиль,
	|	Реквизиты.КоличествоТопливо КАК Количество,
	|	Реквизиты.Сумма КАК Стоимость
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидРасхода = ЗНАЧЕНИЕ(Справочник.ВидыРасходов.Топливо)
	|	И Реквизиты.КоличествоТопливо <> 0";

	Возврат ТекстЗапроса + дт_ОбщегоНазначенияВызовСервераПовтИсп.ТекстРазделителяЗапросовПакета();
	

КонецФункции // ТекстЗапросаРеквизиты()

Функция ТекстЗапросаСебестоимостьПеревозок(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаСебестоимостьПеревозок", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Автомобиль КАК Автомобиль,
	|	Реквизиты.МаршрутныйЛист КАК Документ,
	|	Реквизиты.Сумма КАК Сумма
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидРасхода <> ЗНАЧЕНИЕ(Справочник.ВидыРасходов.Топливо)
	|	И Реквизиты.Автомобиль <> ЗНАЧЕНИЕ(Справочник.Машины.ПустаяСсылка)";

	Возврат ТекстЗапроса + дт_ОбщегоНазначенияВызовСервераПовтИсп.ТекстРазделителяЗапросовПакета();
	

КонецФункции // ТекстЗапросаРеквизиты()

Функция ТекстЗапросаРасчетыСПоставщиками(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРасчетыСПоставщиками", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Основание КАК Сделка,
	|	Реквизиты.Сумма КАК Сумма
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|ГДЕ
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщикуЗапчастей)";

	Возврат ТекстЗапроса + дт_ОбщегоНазначенияВызовСервераПовтИсп.ТекстРазделителяЗапросовПакета();
	

КонецФункции // ТекстЗапросаРеквизиты()


#КонецОбласти 


#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти

#КонецЕсли


