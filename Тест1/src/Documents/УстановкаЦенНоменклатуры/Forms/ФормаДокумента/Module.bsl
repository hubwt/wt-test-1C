

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ДоступноЗаполнениеПоНоменклатуре = Пользователи.ЭтоПолноправныйПользователь();
	ДоступныФиксированныеЦены = ДоступноЗаполнениеПоНоменклатуре ИЛИ РольДоступна("дт_ИзменениеФиксированныхЦен");
	
	Если НЕ ДоступноЗаполнениеПоНоменклатуре Тогда
		
		ЗаполнитьДоступныеТипыЦен();
		
		Если ДоступныеТипыЦен <> Неопределено Тогда
			Элементы.ТипЦен.СписокВыбора.ЗагрузитьЗначения(ДоступныеТипыЦен.ВыгрузитьЗначения());
		КонецЕсли;
		Элементы.ТипЦен.РежимВыбораИзСписка = Истина;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры




&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры



#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ДатаПриИзмененииСервер();
	КонецЕсли;	
КонецПроцедуры


&НаСервере
Процедура ДатаПриИзмененииСервер()

	Если дт_Нумерация.ГодИзменен(Объект.Ссылка, Объект.Дата) Тогда
		Объект.Номер = "";
	КонецЕсли;

КонецПроцедуры // ДатаПриИзмененииСервер()


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Товары

&НаСервереБезКонтекста
Процедура ТоварыНоменклатураПриИзмененииНаСервере(ДанныеЗаполнения)

	ДанныеЗаполнения.Цена = дт_Ценообразование.ПолучитьЦену(ДанныеЗаполнения, ДанныеЗаполнения.Дата, ДанныеЗаполнения.ТипЦен);

КонецПроцедуры


&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Дата",			Объект.Дата);
	ДанныеЗаполнения.Вставить("ТипЦен",			Объект.ТипЦен);
	ДанныеЗаполнения.Вставить("Номенклатура",	Элементы.Товары.ТекущиеДанные.Номенклатура);
	ДанныеЗаполнения.Вставить("Цена",			0);
	
	ТоварыНоменклатураПриИзмененииНаСервере(ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(Элементы.Товары.ТекущиеДанные, ДанныеЗаполнения, , "Номенклатура");
	
КонецПроцедуры




&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ЗаполнитьПоНоменклатуреНаСервере()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	СпрНоменклатура.РекомендованаяЦена КАК Цена
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	ВЫБОР
		|		КОГДА СпрНоменклатура.ЦенаФиксирована
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыЦен.Фиксированная)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыЦен.Рекомендованная)
		|	КОНЕЦ = &ТипЦен";
	
	Запрос.УстановитьПараметр("ТипЦен", Объект.ТипЦен);
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоНоменклатуре(Команда)
	ЗаполнитьПоНоменклатуреНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		Оповестить("Запись_Цена", , ЭтаФорма);
	КонецЕсли;
	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.ТоварыЗаполнитьПоНоменклатуре.Видимость = Форма.ДоступноЗаполнениеПоНоменклатуре;
	Если НЕ Форма.ДоступныФиксированныеЦены
		И Объект.ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦен.Фиксированная") Тогда
			
			Элементы.ТипЦен.Вид = ВидПоляФормы.ПолеНадписи;
			Форма.ТолькоПросмотр = Истина;
			
	КонецЕсли;
КонецПроцедуры // УправлениеФормой()

&НаСервере
Процедура ЗаполнитьДоступныеТипыЦен() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТипыЦен.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыЦен КАК ТипыЦен
		|ГДЕ
		|	ТипыЦен.Ссылка <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.Фиксированная)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТипыЦен.Ссылка
		|ИЗ
		|	Справочник.ТипыЦен КАК ТипыЦен
		|ГДЕ
		|	ТипыЦен.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыЦен.Фиксированная)
		|	И &ДоступныФиксированныеЦены
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДоступныФиксированныеЦены", ДоступныФиксированныеЦены);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДоступныеТипыЦен.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

#КонецОбласти