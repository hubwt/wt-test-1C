
Функция ИмяФайлаШрифта()

	Возврат "rouble.otf";

КонецФункции // ИмяФайлаШрифта()


Функция ШрифтУстановлен() Экспорт
	
	КаталогиШрифтов = Новый Массив();
	
	Попытка
		WshShell     = Новый COMОбъект("WScript.Shell");
		// общие шрифты
		КаталогиШрифтов.Добавить(WshShell.SpecialFolders.Item("Fonts"));
		
		// локальные шрифты: C:\Users\...\AppData\Local\Microsoft\Windows\Fonts
		ИмяКаталога = WshShell.SpecialFolders.Item("Templates");
		
		// не делайте так :(
		ИмяКаталога = СтрЗаменить(ИмяКаталога, "Roaming", "Local");
		ИмяКаталога = СтрЗаменить(ИмяКаталога, "Templates", "Fonts");
		
		КаталогиШрифтов.Добавить(ИмяКаталога);
	Исключение
	КонецПопытки;
	
	Результат = Ложь;
	Для каждого ИмяКаталога Из КаталогиШрифтов Цикл
	
		Файл = Новый Файл(ИмяКаталога + "\" + ИмяФайлаШрифта());
	    Результат = Результат ИЛИ Файл.Существует();
		Если Результат Тогда
			Прервать
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

Процедура ПриНачалеРаботыСистемы() Экспорт

	ТипПлатформыКлиента =  ОбщегоНазначенияКлиент.ТипПлатформыКлиента();
	Если НЕ ТипПлатформыКлиента = ТипПлатформы.Windows_x86
		И НЕ ТипПлатформыКлиента = ТипПлатформы.Windows_x86_64 Тогда
		
		Возврат
		
	КонецЕсли;
	
	Если ШрифтУстановлен() Тогда
		Возврат
	КонецЕсли;

	Попытка
		СистемнаяИнформация = Новый СистемнаяИнформация; 
		ВерсияОС = СистемнаяИнформация.ВерсияОС;
		ИмяФайла = ИмяФайлаШрифта();
		Макет = дт_ШрифтРубляВызовСервера.ПолучитьШрифт();
		Каталог = КаталогВременныхФайлов();
		ПолноеИмяФайла = Каталог + ИмяФайла;
		Макет.Записать(ПолноеИмяФайла);
		
		
		Если Найти(ВерсияОС, "Microsoft Windows XP") = 0 Тогда
			objShell = Новый COMОбъект("Shell.Application");			    
			objNameSpace = objShell.Namespace(Каталог);
			objFont      = objNameSpace.ParseName(ИмяФайла);
			objFont.InvokeVerb("Install");
			objFont = Неопределено;
		Иначе
			WshShell     = Новый COMОбъект("WScript.Shell");
			FSO          = Новый COMОбъект("Scripting.FileSystemObject");
			ПутьШрифты     = wshShell.SpecialFolders.Item("Fonts");
			ПолноеИмяФайла = ПутьШрифты + "\" + ИмяФайла;
			Макет.Записать(ПолноеИмяФайла);
			WshShell.Run("RunDll32.exe gdi32.dll,AddFontResourceA " + FSO.GetBaseName(ПолноеИмяФайла));
		КонецЕсли;
		
		//УдалитьФайлы(ПолноеИмяФайла);
	Исключение
		
		ТекстОшибки =  "Не удалось зарегистрировать шрифт. Зарегистрируйте его вручную. Файл шрифта: "+ПолноеИмяФайла+Символы.ПС+ОписаниеОшибки();
		Сообщить(ТекстОшибки);
		//ЗаписьЖурналаРегистрации(
		//	"Сеанс.Начало", 
		//	УровеньЖурналаРегистрации.Ошибка, 
		//	,
		//	,
		//	ТекстОшибки
		//);
	КонецПопытки;	
	
КонецПроцедуры //ПриНачалеРаботыСистемы()
