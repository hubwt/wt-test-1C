
#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииОбщегоНазначения

// Функция преобразует строку к множественному числу
//
// Параметры: 
//  Слово1 - форма слова в ед числе      ("шкаф")
//  Слово2 - форма слова для числит 2-4  ("шкафа")
//  Слово3 - форма слова для числит 5-10 ("шкафов")
//  ЦелоеЧисло - целое число
//
// Возвращаемое значение:
//  строку - одну из строк в зависимости от параметра ЦелоеЧисло
//
// Описание:
//  Предназначена для формирования "правильной" подписи к числительным
//
Функция ФормаМножественногоЧисла(Слово1, Слово2, Слово3, Знач ЦелоеЧисло) Экспорт
	
	// Изменим знак целого числа, иначе отрицательные числа будут неправильно преобразовываться
	Если ЦелоеЧисло < 0 Тогда
		ЦелоеЧисло = -1 * ЦелоеЧисло;
	КонецЕсли;
	
	Если ЦелоеЧисло <> Цел(ЦелоеЧисло) Тогда 
		// для нецелых чисел - всегда вторая форма
		Возврат Слово2;
	КонецЕсли;
	
	// остаток
	Остаток = ЦелоеЧисло%10;
	Если (ЦелоеЧисло >10) И (ЦелоеЧисло<20) Тогда
		// для второго десятка - всегда третья форма
		Возврат Слово3;
	ИначеЕсли Остаток=1 Тогда
		Возврат Слово1;
	ИначеЕсли (Остаток>1) И (Остаток<5) Тогда
		Возврат Слово2;
	Иначе
		Возврат Слово3;
	КонецЕсли;

КонецФункции

// Определяет, является ли контрагент юридическим лицом
//
// Параметры:
//	ЮридическоеФизическоеЛицо - Перечисления.ЮридическоеФизическоеЛицо - вид контрагента
//
// Возвращаемое значение:
//  Булево - Истина, если это юридическое лицо, Ложь в обратном случае.
//
Функция ЭтоЮрЛицо(ЮридическоеФизическоеЛицо) Экспорт
	
	Возврат ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.дт_ТипыКлиентов.ЮрЛицо");

КонецФункции
	
	
Функция ЭтоИП(ТипКлиента) Экспорт
	
	Возврат ТипКлиента = ПредопределенноеЗначение("Перечисление.дт_ТипыКлиентов.ИП");
	
КонецФункции

Функция ЭтоФизлицо(ТипКлиента) Экспорт

	Возврат ТипКлиента = ПредопределенноеЗначение("Перечисление.дт_ТипыКлиентов.ФизЛицо");
	
КонецФункции

Функция ЭтоЧастноеЛицо(ТипКлиента) Экспорт
	
	Возврат ЭтоФизлицо(ТипКлиента) ИЛИ ЭтоИП(ТипКлиента);
	
КонецФункции

Функция ЭтоБизнесКлиент(ТипКлиента) Экспорт
	
	Возврат ЭтоЮрЛицо(ТипКлиента) ИЛИ ЭтоИП(ТипКлиента);

КонецФункции	
	
	
// Возвращает слово в разных склонениях (в соответствии с числом)
// Основное назначение - использование для склонения надписей единиц измерения в формах
// для различных числовых полей
//
// Параметры
//	Число - Число - число, определяет склонение
//	ЕдЧислоИменительный - Строка - единственное число именительный падеж. Например, "день" или "неделя"
//	ЕдЧислоРодительный - Строка - единственное число родительный падеж. Например, "дня" или "недели"
//	МнЧислоРодительный - Строка - множественное число родительный падеж. Например, "дней" или "недель"
//	Пол - Строка - м - мужской род (ж - женский род, с - средний род); 
//
// Возвращаемое значение:
//  СловоПрописью - Строка - слово в правильном склонении
//
Функция СклонениеСлова(ЧислоДляСклонения, ЕдЧислоИменительный, ЕдЧислоРодительный, МнЧислоРодительный, Пол) Экспорт
	
	СловоПрописью = "";
	ФорматнаяСтрока = "L = ru_RU";
	ПараметрыПредметаИсчисления = ЕдЧислоИменительный + ", " + ЕдЧислоРодительный + ", " + МнЧислоРодительный + ", " + Пол;
	
	СловоПрописью = ЧислоПрописью(ЧислоДляСклонения, ФорматнаяСтрока, ПараметрыПредметаИсчисления);
	
	Если СтрНайти(СловоПрописью, МнЧислоРодительный) Тогда
		СловоПрописью = МнЧислоРодительный;
	ИначеЕсли СтрНайти(СловоПрописью, ЕдЧислоРодительный) Тогда
		СловоПрописью = ЕдЧислоРодительный;
	ИначеЕсли СтрНайти(СловоПрописью, ЕдЧислоИменительный) Тогда
		СловоПрописью = ЕдЧислоИменительный;
	КонецЕсли;
	
	Возврат СловоПрописью;
	
КонецФункции

#КонецОбласти
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
