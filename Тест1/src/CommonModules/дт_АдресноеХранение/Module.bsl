#Область ПрограммныйИнтерфейс

// Выполняет движения регистра накопления дт_НачислениеЗарплатыУУ
//
Процедура ОтразитьДвижения(ПараметрыПроведения, Движения, Отказ) Экспорт
	
	ТаблицаДвижения = ПараметрыПроведения.ТаблицаМестаХраненияНоменклатуры;
	Если Отказ
	 ИЛИ ТаблицаДвижения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	
	ДвиженияНабор = Движения.дт_МестаХраненияНоменклатуры;
	ДвиженияНабор.Записывать = Истина;
	ДвиженияНабор.Загрузить(ТаблицаДвижения);
	
КонецПроцедуры

// Готовит к записи наборы записей документа (только по регистрам, входящих в подсистему)
//
Процедура ПодготовитьНаборыЗаписей(СтруктураОбъект) Экспорт
	
	РегистрыПодсистемы = Новый Массив;
	РегистрыПодсистемы.Добавить("дт_МестаХраненияНоменклатуры");
	
	Для каждого Регистр Из РегистрыПодсистемы Цикл
	
		НаборЗаписей = СтруктураОбъект.Движения.Найти(Регистр);
		
		Если НаборЗаписей <> Неопределено Тогда
			Если НЕ НаборЗаписей.ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
				
			КонецЕсли;
			
			Если НЕ НаборЗаписей.ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
				
				НаборЗаписей.ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", СтруктураОбъект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы);
				
			КонецЕсли;
			
			//РегистрыНакопления[Регистр].СоздатьПустуюВременнуюТаблицуИзменение(СтруктураОбъект.ДополнительныеСвойства);
		
		КонецЕсли;
	
	КонецЦикла; 
	
КонецПроцедуры

Функция ПолучитьМестоХранения(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаМестоХранения();
	
	Запрос.УстановитьПараметр("ДатаСреза", Отбор.Дата);
	Запрос.УстановитьПараметр("Склад", Отбор.Склад);
	Запрос.УстановитьПараметр("Номенклатура", Отбор.Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Результат = Неопределено;
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Результат = ВыборкаДетальныеЗаписи.МестоХранения;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьГруппуМестХранения(Отбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаМестоХранения();
	
	Запрос.УстановитьПараметр("ДатаСреза", Отбор.Дата);
	Запрос.УстановитьПараметр("Склад", Отбор.Склад);
	Запрос.УстановитьПараметр("Номенклатура", Отбор.Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Результат = Неопределено;
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Результат = ВыборкаДетальныеЗаписи.ГруппаМестХранения;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьМестаХранения(Форма, ИмяПоляСклад, ИмяТЧ, ИмяПоляНоменклатура) Экспорт
	
	Объект = Форма.Объект;
	СкладВТЧ = Ложь;
	
	Индекс = СтрНайти(ИмяПоляСклад, ".");
	Если Индекс <> 0
		И СтрНачинаетсяС(ИмяПоляСклад, ИмяТЧ + ".") Тогда
			
		СкладВТЧ = Истина;
		
		ИмяПоляСкладТЧ = Сред(ИмяПоляСклад, Индекс + 1);
			
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Товары." + ИмяПоляНоменклатура + " КАК Номенклатура,
		|	Товары.НомерСтроки" + ?(СкладВТЧ, ", Товары." + ИмяПоляСкладТЧ + " КАК Склад", "") + "
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура,
		|	втТовары.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения, ЗНАЧЕНИЕ(Справочник.Стеллаж.ПустаяСсылка)) КАК
		|		МестоХранения,
		|	ЕСТЬNULL(Стеллаж.ГруппаМестХранения, ЗНАЧЕНИЕ(Справочник.ГруппыМестХранения.ПустаяСсылка)) КАК ГруппаМестХранения
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.дт_МестаХраненияНоменклатуры.СрезПоследних(&Дата,  
		| "	+ ?(СкладВТЧ, "", "Склад = &Склад") + ") КАК дт_МестаХраненияНоменклатурыСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Стеллаж КАК Стеллаж
		|			ПО дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения = Стеллаж.Ссылка
		|		ПО втТовары.Номенклатура = дт_МестаХраненияНоменклатурыСрезПоследних.Номенклатура
		|	" + ?(СкладВТЧ, "И втТовары.Склад = дт_МестаХраненияНоменклатурыСрезПоследних.Склад", "") + "
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Товары", Объект[ИмяТЧ].Выгрузить());
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());//Объект.Дата);
	Если НЕ СкладВТЧ Тогда
		Запрос.УстановитьПараметр("Склад", Объект[ИмяПоляСклад]);
	КонецЕсли;	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	ДанныеСтроки = Новый Структура();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки.Вставить("МестоХранения", Выборка.МестоХранения);
		ДанныеСтроки.Вставить("ГруппаМестХранения", Выборка.ГруппаМестХранения);
		
		СтрокаТовары = Объект[ИмяТЧ][Выборка.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(СтрокаТовары, ДанныеСтроки);
		
	КонецЦикла;
	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаМестоХранения()
	Возврат 
		"ВЫБРАТЬ
		|	дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения КАК МестоХранения,
		|	ЕСТЬNULL(Стеллаж.ГруппаМестХранения, ЗНАЧЕНИЕ(Справочник.ГруппыМестХранения.ПустаяСсылка)) КАК ГруппаМестХранения
		|ИЗ
		|	РегистрСведений.дт_МестаХраненияНоменклатуры.СрезПоследних(&ДатаСреза, Склад = &Склад
		|	И Номенклатура = &Номенклатура) КАК дт_МестаХраненияНоменклатурыСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Стеллаж КАК Стеллаж
		|		ПО дт_МестаХраненияНоменклатурыСрезПоследних.МестоХранения = Стеллаж.Ссылка";
	
КонецФункции

#КонецОбласти