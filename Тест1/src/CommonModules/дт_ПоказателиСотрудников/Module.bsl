 #Область ПрограммныйИнтерфейс
 
 Процедура ЗаполнитьПоказателиСотрудников(ТаблицаСотрудники, Отказ = Ложь) Экспорт
 	
 	Запрос = Новый Запрос;
 	Запрос.Текст =
 		"ВЫБРАТЬ
 		|	ВременнаяТаблица.Сотрудник,
 		|	ВременнаяТаблица.Подразделение,
 		|	ВременнаяТаблица.Должность
 		|ПОМЕСТИТЬ втСотрудники
 		|ИЗ
 		|	&ВременнаяТаблица КАК ВременнаяТаблица
 		|;
 		|
 		|////////////////////////////////////////////////////////////////////////////////
 		|ВЫБРАТЬ
 		|	втСотрудники.Сотрудник,
 		|	ДолжностиПредприятия.ПрофильПоказателей
 		|ПОМЕСТИТЬ втПрофилиСотрудников
 		|ИЗ
 		|	Справочник.ДолжностиПредприятия КАК ДолжностиПредприятия
 		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
 		|		ПО втСотрудники.Должность = ДолжностиПредприятия.Ссылка
 		|		И ДолжностиПредприятия.ПрофильПоказателей <> ЗНАЧЕНИЕ(Справочник.ПрофилиПоказателей.ПустаяСсылка)
 		|;
 		|
 		|////////////////////////////////////////////////////////////////////////////////
 		|ВЫБРАТЬ
 		|	ПрофилиПоказателейПоказатели.Показатель,
 		|	втПрофилиСотрудников.Сотрудник КАК Сотрудник
 		|ИЗ
 		|	Справочник.ПрофилиПоказателей.Показатели КАК ПрофилиПоказателейПоказатели
 		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПрофилиСотрудников КАК втПрофилиСотрудников
 		|		ПО втПрофилиСотрудников.ПрофильПоказателей = ПрофилиПоказателейПоказатели.Ссылка
 		|ИТОГИ
 		|ПО
 		|	Сотрудник";
 	
 	Запрос.УстановитьПараметр("ВременнаяТаблица", ТаблицаСотрудники);
 	
 	РезультатЗапроса = Запрос.Выполнить();
 	
 	ВыборкаСотрудник = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
 	
 	НачатьТранзакцию();
 	
 	Пока ВыборкаСотрудник.Следующий() Цикл
 		
		Спр = ВыборкаСотрудник.Сотрудник.ПолучитьОбъект();
		
		Попытка
			Спр.Заблокировать();
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			Отказ = Истина;
			Прервать;
		КонецПопытки;
		
		СтрокиКУдалению = Спр.Показатели.НайтиСтроки(Новый Структура("ЭтоДополнительный", Ложь));
		Для Индекс = 0 По СтрокиКУдалению.Количество() - 1 Цикл
			Спр.Показатели.Удалить(СтрокиКУдалению[Индекс]);
		КонецЦикла;
		
 	
 		ВыборкаДетальныеЗаписи = ВыборкаСотрудник.Выбрать();
 	
 		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
 			НоваяСтрока = Спр.Показатели.Добавить();
 			НоваяСтрока.Показатель = ВыборкаДетальныеЗаписи.Показатель;
 		КонецЦикла;
 		
 		Попытка
 			Спр.Записать();
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			Отказ = Истина;
			Прервать;
		КонецПопытки;
 			
 		
 	КонецЦикла;
 	
 	Если ТранзакцияАктивна() Тогда
 		Если Отказ Тогда
 			ОтменитьТранзакцию();
 		Иначе
 			ЗафиксироватьТранзакцию();
 		КонецЕсли;
 	КонецЕсли;
 	
 	Если Отказ Тогда
 		ВызватьИсключение ТекстОшибки;
 	КонецЕсли;
 	
 КонецПроцедуры
 
 
 Функция ПоказателиУстановлены(Сотрудник = Неопределено, Дата = Неопределено) Экспорт
 	
 	Если Сотрудник = Неопределено Тогда
 		Сотрудник = дт_Сотрудники.СотрудникПоПользователю(Пользователи.ТекущийПользователь());
 	КонецЕсли;
 	
 	Если Дата = Неопределено Тогда
 		Дата = ТекущаяДатаСеанса();
 	КонецЕсли;
 	
 	
 	Запрос = Новый Запрос;
 	Запрос.Текст =
 		"ВЫБРАТЬ
 		|	СотрудникиПоказатели.Показатель,
 		|	СотрудникиПоказатели.Ссылка
 		|ПОМЕСТИТЬ вПоказателиСотрудника
 		|ИЗ
 		|	Справочник.Сотрудники.Показатели КАК СотрудникиПоказатели
 		|ГДЕ
 		|	СотрудникиПоказатели.Ссылка = &Сотрудник
 		|;
 		|
 		|////////////////////////////////////////////////////////////////////////////////
 		|ВЫБРАТЬ
 		|	вПоказателиСотрудника.Показатель
 		|ИЗ
 		|	вПоказателиСотрудника КАК вПоказателиСотрудника
 		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияПоказателейСотрудников КАК ЗначенияПоказателейСотрудников
 		|		ПО ЗначенияПоказателейСотрудников.Сотрудник = вПоказателиСотрудника.Ссылка
 		|		И вПоказателиСотрудника.Показатель = ЗначенияПоказателейСотрудников.Показатель
 		|		И ЗначенияПоказателейСотрудников.Период = &Период
 		|ГДЕ
 		|	ЗначенияПоказателейСотрудников.Значение ЕСТЬ NULL";
 	
 	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
 	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
 	
 	РезультатЗапроса = Запрос.Выполнить();
 	
 	Возврат РезультатЗапроса.Пустой();
 	
 КонецФункции
 
 Функция ПолучитьВремяУстановкиПоказателей() Экспорт
 	Возврат Константы.дт_ВремяУстановкиПоказателейСотрудников.Получить();
 КонецФункции
 
 #КонецОбласти
 
 #Область СлужебныйПрограммныйИнтерфейс
 
 
 
 #КонецОбласти
 
 #Область СлужебныеПроцедурыИФункции
 
 
 
 #КонецОбласти