
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатыПоУмолчанию();
	ПолучитьСписокЛиний();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьОтменитьПриИзменении(Элемент)
	СброситьЗаполнитьСписокЛиний();	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСписокЛиний()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		//|ВЫБОР
		//|	КОГДА ЛинияНомер = """"
		//|   	ТОГДА ""<Пустое значение>""
		//|	ИНАЧЕ ЛинияНомер
		//|КОНЕЦ КАК ЛинияНомер
		| ЛинияНомер
		|	
		|ИЗ
		|  РегистрСведений.МаршрутЗвонков
		|
		|ГДЕ
		|	ЛинияНомер <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЛинияНомер
		|";
		
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	ВыборЛиний = Новый СписокЗначений;
	ВыборЛиний.ЗагрузитьЗначения(ТЗ.ВыгрузитьКолонку("ЛинияНомер"));
	
КонецПроцедуры

&НаСервере
Процедура СброситьЗаполнитьСписокЛиний()
	
	Для Каждого эл ИЗ ВыборЛиний Цикл
		эл.Пометка = ОтметитьОтменить;		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыбранныйСписокЛиний()
	
	СписокВозврата = Новый СписокЗначений();
	Для Каждого эл ИЗ ВыборЛиний Цикл
		Если эл.Пометка Тогда
			СписокВозврата.Добавить(эл.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокВозврата;
	
КонецФункции

&НаСервере
Процедура ДатыПоУмолчанию()	
	НачальнаяДата 	= НачалоМесяца(ТекущаяДата());
	КонечнаяДата 	= КонецДня(ТекущаяДата());
КонецПроцедуры

&НаСервере
Функция ПолучитьВоронку()
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачальнаяДата", НачалоДня(НачальнаяДата));
	Запрос.УстановитьПараметр("КонечнаяДата", КонецДня(КонечнаяДата));
	Запрос.УстановитьПараметр("ВыбранныеЛинии", ВыбранныйСписокЛиний());
	Запрос.Текст = "ВЫБРАТЬ
		|///////////// Звонки
		|	МаршрутЗвонков.ЛинияНомер КАК ЛинияНомер,
		|	КОЛИЧЕСТВО(МаршрутЗвонков.ЛинияНомер) КАК Количество
		|	ПОМЕСТИТЬ табКоличествоЗвонков
		|
		|ИЗ
		|  РегистрСведений.МаршрутЗвонков КАК МаршрутЗвонков
		|
		|ГДЕ
		|	МаршрутЗвонков.Дата МЕЖДУ &НачальнаяДата И &КонечнаяДата	
		|	И МаршрутЗвонков.ЛинияНомер В (&ВыбранныеЛинии) 
		|
		|СГРУППИРОВАТЬ ПО
		|	ЛинияНомер
		|;
		|////// Список линий
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокЛиний.ЛинияНомер 
		|	ПОМЕСТИТЬ табСписокЛиний
		|
		|ИЗ
		|  РегистрСведений.МаршрутЗвонков КАК СписокЛиний
		|;
		|/////// Заявки
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Заявка.Ссылка) КАК Количество,
		|   МаршрутЗвонков.ЛинияНомер КАК ЛинияНомер
		|	ПОМЕСТИТЬ табКоличествоЗаявок
		|
		|ИЗ
		|	Документ.ЗаказКлиента КАК Заявка	
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.МаршрутЗвонков КАК МаршрутЗвонков	
		|ПО
		|	Заявка.Ссылка = МаршрутЗвонков.ЗаявкаКлиента
		|
		|ГДЕ
		|	Заявка.Дата МЕЖДУ &НачальнаяДата И &КонечнаяДата
		|	И МаршрутЗвонков.ЛинияНомер В (&ВыбранныеЛинии) 
		|
		|СГРУППИРОВАТЬ ПО
		|	ЛинияНомер
		|;
		|//////////// Продажи
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Продажа.Ссылка) КАК Количество,
		|   МаршрутЗвонков.ЛинияНомер КАК ЛинияНомер
		|	ПОМЕСТИТЬ табКоличествоПродаж
		|
		|ИЗ
		|	Документ.ПродажаЗапчастей КАК Продажа
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.МаршрутЗвонков КАК МаршрутЗвонков	
		|ПО
		|	Продажа.Ссылка.ЗаказКлиента = МаршрутЗвонков.ЗаявкаКлиента
		|
		|ГДЕ
		|	Продажа.Дата МЕЖДУ &НачальнаяДата И &КонечнаяДата
		|	И МаршрутЗвонков.ЛинияНомер В (&ВыбранныеЛинии) 
		|
		|СГРУППИРОВАТЬ ПО
		|	ЛинияНомер
		|;
		|/////////// Итоговая таблица
		|ВЫБРАТЬ
		//|	Итог.ЛинияНомер КАК ЛинияНомер,
		|	ИтогКоличествоЗвонков.Количество КАК КоличествоЗвонков,
		|   ИтогКоличествоЗаявок.Количество КАК КоличествоЗаявок,
		|	ИтогКоличествоПродаж.Количество КАК КоличествоПродаж
		|
		|ИЗ
		|	табСписокЛиний КАК Итог
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	табКоличествоЗвонков КАК ИтогКоличествоЗвонков
		|ПО
		|	Итог.ЛинияНомер = ИтогКоличествоЗвонков.ЛинияНомер
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	табКоличествоЗаявок КАК ИтогКоличествоЗаявок	
		|ПО
		|   Итог.ЛинияНомер = ИтогКоличествоЗаявок.ЛинияНомер
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	табКоличествоПродаж КАК ИтогКоличествоПродаж
		|ПО
		|	Итог.ЛинияНомер = ИтогКоличествоПродаж.ЛинияНомер
		|
		|ГДЕ
		|	Итог.ЛинияНомер В (&ВыбранныеЛинии)	
		|
		|ИТОГИ
		|	СУММА(ИтогКоличествоЗвонков.Количество),
		|	СУММА(ИтогКоличествоЗаявок.Количество),
		|	СУММА(ИтогКоличествоПродаж.Количество)
		|ПО
		|	ОБЩИЕ 
		|
		|";
		
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Если ТЗ.Количество() = 0 Тогда
		Возврат null;
	Иначе
		Возврат ТЗ[0];
	КонецЕсли;
		
КонецФункции

&НаСервере
Процедура ОбновитьДиаграмму()
	
	Диаграмма = ЭтотОбъект.ВыводДиаграмма;
	Диаграмма.Очистить();
	
	ДанныеДляДиаграммы = ПолучитьВоронку();
	
	Если ДанныеДляДиаграммы = null Тогда
		Возврат;	
	КонецЕсли;
	
	КолЗвонков 	= ?(ДанныеДляДиаграммы.КоличествоЗвонков = null, 0, ДанныеДляДиаграммы.КоличествоЗвонков);
	КолЗаявок	= ?(ДанныеДляДиаграммы.КоличествоЗаявок = null, 0, ДанныеДляДиаграммы.КоличествоЗаявок);
	КолПродаж	= ?(ДанныеДляДиаграммы.КоличествоПродаж = null, 0, ДанныеДляДиаграммы.КоличествоПродаж);
	
	ПроцентЗаявок = ?(КолЗвонков = 0, 0, Окр(КолЗаявок / КолЗвонков * 100, 0));
	ПроцентПродаж = ?(КолЗвонков = 0, 0, Окр(КолПродаж / КолЗвонков * 100, 0));
	
	Диаграмма.АвтоТранспонирование 	= Истина;
	Диаграмма.Обновление 			= Ложь;
	
	ТекстЗвонки = "ЗВОНКИ" + Символы.ПС +
					Символы.ПС +
					"Количество: " + КолЗвонков + Символы.ПС + 
					"Процент: 100 %";
	
	ТекстЗаявки = "ЗАЯВКИ" + Символы.ПС + 
					Символы.ПС +
					"Количество: " + КолЗаявок + Символы.ПС +
					"Процент: " + ПроцентЗаявок + " %";
	
	ТекстПродажи = "ПРОДАЖИ
					|
					|Количество: " + КолПродаж + "
					|Процент: " + ПроцентПродаж + " %";
		
	Диаграмма.Серии.Добавить(ТекстЗвонки);
	Диаграмма.Серии[0].Цвет = WebЦвета.Голубой;
	Диаграмма.Серии.Добавить(ТекстЗаявки);
	Диаграмма.Серии[1].Цвет = WebЦвета.Желтый;
	Диаграмма.Серии.Добавить(ТекстПродажи);
	Диаграмма.Серии[2].Цвет = WebЦвета.СветлоЗеленый;
	
	Точка = Диаграмма.УстановитьТочку("Воронка");
		
	Диаграмма.УстановитьЗначение(Точка
			, Диаграмма.Серии[0]
			, КолЗвонков
			, Неопределено
			, "Звонки");				
	Диаграмма.УстановитьЗначение(Точка
			, Диаграмма.Серии[1]
			, КолЗаявок
			, Неопределено
			, "Заявки");	
	Диаграмма.УстановитьЗначение(Точка
			, Диаграмма.Серии[2]
			, КолПродаж
			, Неопределено
			, "Продажи");
	
	Диаграмма.ОбластьЗаголовка.Текст 	= "Воронка продаж";
	Диаграмма.АвтоТранспонирование 		= Истина;
	Диаграмма.Обновление				= Истина;
	
КонецПроцедуры